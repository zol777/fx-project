// use gradlew -Penv=${env} to pass
def env = hasProperty('env') ? env : null

apply plugin: 'application'
mainClassName = 'Main'

tasks.named("processResources").configure {
    if (env != null) {
        assert project.file("conf/${env}/resources").exists()
        inputs.dir project.file("conf/${env}/resources")
        from "conf/${env}/resources"
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

tasks.named("distTar").configure {
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}

tasks.named("distZip").configure {
    enabled = false
}

tasks.named("startScripts").configure {
    defaultJvmOpts = ['-Dcore.webPath=APP_HOME_VAR/web', "-Dcore.appName=${applicationName}"]

    doLast {
        windowsScript.text = windowsScript.text.replaceFirst('APP_HOME_VAR', '%APP_HOME%')
        unixScript.text = unixScript.text.replaceFirst('APP_HOME_VAR', '\\$APP_HOME')
    }
}

tasks.named("run").configure {
    // assign all system properties to application, e.g. ./gradlew -Dkey=value :some-service:run
    System.properties.each { key, value ->
        if (key != "user.dir" && key != "java.endorsed.dirs") {
            systemProperty key as String, value
        }
    }
}

afterEvaluate {
    tasks.register('moveTarToDocker') {
        group = 'distribution'
        doLast {
            delete "${projectDir}/docker/file/${project.name}.tar"
            copy {
                from("${buildDir}/distributions/${project.name}.tar")
                into("${projectDir}/docker/file")
            }
        }
    }
}

